import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

def changelogText = rootProject.file('changelog.md').text

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Illusive Soulworks maven"
        url = "https://maven.theillusivec4.top/"
    }
}

dependencies {

    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("curse.maven:sophisticated-backpacks-422301:6587116")
    implementation fg.deobf("curse.maven:sophisticated-core-618298:6596375")
    implementation fg.deobf("curse.maven:travelers-backpack-321117:6650344")
    compileOnly "top.theillusivec4.curios:curios-forge:${curios_version}:api"
}


tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}


tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : mod_version,
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    finalizedBy 'reobfJar'
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    dependsOn jar

    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload('1288945', file("build/libs/${mod_id}-${mod_version}.jar"))
    mainFile.displayName = "${mod_name}-${mod_version}"
    mainFile.releaseType = 'release'
    mainFile.changelog = changelogText

    mainFile.addOptional("sophisticated-backpacks")
    mainFile.addOptional("travelers-backpack")
    mainFile.addOptional("curios")
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "ezweight"
    versionName = "${mod_name} ${mod_version}"
    versionNumber = "${mod_version}"
    versionType = "release"
    uploadFile = file("build/libs/${mod_id}-${mod_version}.jar")
    gameVersions = ["${minecraft_version}"] as Iterable<? extends String>
    loaders = ["forge"]
    changelog = changelogText


    dependencies {
        optional.project 'sophisticated-backpacks'
        optional.project 'travelersbackpack'
        optional.project 'curios'
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.register('publicModdedSite') {
    dependsOn 'build'
    dependsOn 'publishCurseForge'
    dependsOn 'modrinth'
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
